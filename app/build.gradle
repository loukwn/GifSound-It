apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: "org.jlleitschuh.gradle.ktlint"

// Reads the local properties for the App secrets and if it cannot find them, it will get them from
// the empty ones
Properties properties = new Properties()
try {
    properties.load(project.rootProject.file("local.properties").newDataInputStream())
} catch(Exception ignored) {
    properties.load(project.rootProject.file("empty.properties").newDataInputStream())
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId "com.kostaslou.gifsoundit"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 2
        versionName "1.1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-release-rules.pro'

            // keys
            buildConfigField 'String', "RedditClientId", properties.getProperty("GifSoundIt_RedditClientId")
            buildConfigField 'String', "RedditUserAgent", properties.getProperty("GifSoundIt_RedditUserAgent")
        }

        debug {
            debuggable true
            minifyEnabled false
            shrinkResources false

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-debug-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-test-debug-rules.pro'

            // keys
            buildConfigField 'String', "RedditClientId", properties.getProperty("GifSoundIt_RedditClientId")
            buildConfigField 'String', "RedditUserAgent", properties.getProperty("GifSoundIt_RedditUserAgent")
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    androidExtensions {
        experimental = true
    }
}

repositories {
    maven { url 'https://jitpack.io' }
    google()
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // submodules
    implementation project(':list')
    implementation project(":opengs")
    implementation project(":common")
    implementation project(":postdata")

    // kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$rootProject.kotlin_version"
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib:$rootProject.kotlin_version"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$rootProject.kotlin_version"

    // android extensions
    implementation "androidx.appcompat:appcompat:$rootProject.appcompat_version"
    implementation "androidx.navigation:navigation-fragment-ktx:$rootProject.navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$rootProject.navigation_version"
    implementation "androidx.constraintlayout:constraintlayout:$rootProject.constraintlayout_version"
    implementation "com.google.android.material:material:$rootProject.material_version"

    // retrofit
    implementation "com.squareup.retrofit2:retrofit:$rootProject.retrofit2_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$rootProject.retrofit2_version"
    implementation "com.squareup.retrofit2:converter-moshi:$rootProject.moshi_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$rootProject.retrofit_logging_interceptor_version"

    // hilt
    implementation "com.google.dagger:hilt-android:$rootProject.hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$rootProject.hilt_version"
    implementation "androidx.hilt:hilt-lifecycle-viewmodel:$rootProject.hilt_viewmodel_version"
    kapt "androidx.hilt:hilt-compiler:$rootProject.hilt_compiler_version"

    // Timber
    implementation "com.jakewharton.timber:timber:$rootProject.timber_version"

    // Firebase
    implementation platform("com.google.firebase:firebase-bom:$rootProject.firebase_core_version")
    implementation "com.google.firebase:firebase-analytics"
    implementation "com.google.firebase:firebase-crashlytics-ktx"

    // rx
    implementation "io.reactivex.rxjava2:rxandroid:$rootProject.rxAndroid_version"
    implementation "io.reactivex.rxjava2:rxjava:$rootProject.rxJava2_version"

    // Android testing
    // Core library
    androidTestImplementation "androidx.test:core:$rootProject.androidx_test_core_version"

    // AndroidJUnitRunner and JUnit Rules
    androidTestImplementation "androidx.test:runner:$rootProject.test_runner_version"
    androidTestImplementation "androidx.test:rules:$rootProject.test_rules_version"

    // Assertions
    androidTestImplementation "androidx.test.ext:junit:$rootProject.ext_junit_version"
    androidTestImplementation "androidx.test.ext:junit-ktx:$rootProject.ext_junit_version"

    // Espresso dependencies
    androidTestImplementation "androidx.test.espresso:espresso-core:$rootProject.espresso_version"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$rootProject.espresso_version"
}
